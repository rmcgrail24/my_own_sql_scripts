DROP TABLE logtab;

CREATE TABLE logtab (
    code INTEGER,
    text VARCHAR2(4000),
    created_on DATE,
    created_by VARCHAR2(100)
    );

CREATE OR REPLACE PACKAGE log_pkg
IS
   PROCEDURE putline (code_in IN INTEGER, text_in IN VARCHAR2);
END;
/

CREATE OR REPLACE PACKAGE log81_pkg
IS
   PROCEDURE putline (code_in IN INTEGER, text_in IN VARCHAR2);
END;
/

CREATE OR REPLACE PACKAGE BODY log_pkg
IS
   g_version VARCHAR2 (100);

   FUNCTION oraversion RETURN VARCHAR2
   IS
      retval VARCHAR2 (100);
   BEGIN
      SELECT SUBSTR (version, 1, 3)
        INTO retval
        FROM product_component_version
       WHERE UPPER (product) LIKE 'ORACLE7%'
          OR UPPER (product) LIKE 'PERSONAL ORACLE%'
          OR UPPER (product) LIKE 'ORACLE8%';
      RETURN retval;
   END;

   PROCEDURE putline (code_in IN INTEGER, text_in IN VARCHAR2)
   IS
   BEGIN
      IF g_version = '8.1'
      THEN
         log81_pkg.putline (code_in, text_in);
      ELSE
         INSERT INTO logtab
            VALUES (code_in, text_in, SYSDATE, USER);
      END IF;
   END;

BEGIN
   g_version := oraversion;
END log_pkg;
/

CREATE OR REPLACE PACKAGE BODY log81_pkg
IS
   PROCEDURE putline (
      code_in IN INTEGER, text_in IN VARCHAR2)
   IS
      PRAGMA AUTONOMOUS_TRANSACTION;
   BEGIN
      INSERT INTO logtab
         VALUES (code_in, text_in, SYSDATE, USER);
      COMMIT;
   EXCEPTION
      WHEN OTHERS
      THEN
         ROLLBACK;
   END;
END log81_pkg;
/
